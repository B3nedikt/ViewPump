apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'org.jetbrains.dokka'

apply from: 'jacoco.gradle'
apply from: 'publish.gradle'

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 30
    }
    lintOptions {
        // we don't always want to use the latest version of the support library
        disable 'GradleDependency'
        textOutput 'stdout'
        textReport System.getenv('CI') == 'true'
    }
    buildFeatures {
        buildConfig = false
    }
    variantFilter { variant ->
        if (variant.buildType.name == 'debug') {
            variant.setIgnore(true)
        }
    }
}

dependencies {
    compileOnly 'androidx.appcompat:appcompat:1.2.0'
    api 'org.jetbrains.kotlin:kotlin-stdlib:1.4.0'

    testImplementation 'junit:junit:4.13'
    testImplementation 'org.assertj:assertj-core:3.11.0'
    testImplementation 'org.mockito:mockito-core:3.4.6'

    testImplementation 'androidx.annotation:annotation:1.1.0'
    testImplementation 'androidx.test:runner:1.3.0'
}
